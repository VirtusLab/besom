set fallback := true

output-dir := justfile_directory() / ".out"
yaga-version := "0.4.0-SNAPSHOT"

make-output-dir:
  mkdir -p {{output-dir}}

compile-child-lambda:
  scala-cli compile child-lambda

publish-local-assembled-child-lambda: make-output-dir
  #!/usr/bin/env bash
  outputDir="{{output-dir}}/child"
  jarName="child-lambda.jar"
  rm -rf "$outputDir"
  mkdir -p "$outputDir"
  jarPath="${outputDir}/${jarName}"
  scala-cli package -f child-lambda --assembly --preamble=false -o "$jarPath"
  unpackedDir="${outputDir}/unpacked"
  mkdir "$unpackedDir"

  cd "$unpackedDir"
  jar xf "../${jarName}"
  rm -r META-INF/MANIFEST.MF
  cd -

  # Using a java file prevents from adding scala stdlib as declared dependency
  fakePublishSource=/tmp/fakeSource.java
  rm -rf "${fakePublishSource}"
  touch "${fakePublishSource}"

  scala-cli publish local "${fakePublishSource}" --resource-dirs "$unpackedDir" --organization org.virtuslab --name child-lambda_3 --project-version 0.0.1-SNAPSHOT --doc=false

clean-child-lambda:
  scala-cli clean child-lambda

prepare-parent-lambda:
  #!/usr/bin/env bash
  # TODO: Should generate sources from derived child schema
  rm -rf parent-lambda/generated
  mkdir -p parent-lambda/generated/child-lambda
  scala-cli run \
    --dep org.virtuslab::yaga-codegen-aws:{{yaga-version}}\
    --main-class yaga.codegen.aws.runCodegen \
    -- \
    --maven-artifact org.virtuslab:child-lambda_3:0.0.1-SNAPSHOT \
    --package-prefix childlambda \
    --output-dir $(pwd)/parent-lambda/generated/child-lambda/

compile-parent-lambda:
  scala-cli compile parent-lambda

publish-local-assembled-parent-lambda: make-output-dir
  #!/usr/bin/env bash
  outputDir="{{output-dir}}/parent"
  jarName="parent-lambda.jar"
  rm -rf "$outputDir"
  mkdir -p "$outputDir"
  jarPath="${outputDir}/${jarName}"
  scala-cli package -f parent-lambda --assembly --preamble=false -o "$jarPath"
  unpackedDir="${outputDir}/unpacked"
  mkdir "$unpackedDir"

  cd "$unpackedDir"
  jar xf "../${jarName}"
  rm -r META-INF/MANIFEST.MF
  cd -

  # Using a java file prevents from adding scala stdlib as declared dependency
  fakePublishSource=/tmp/fakeSource.java
  rm -rf "${fakePublishSource}"
  touch "${fakePublishSource}"

  scala-cli publish local "${fakePublishSource}" --resource-dirs "$unpackedDir" --organization org.virtuslab --name parent-lambda_3 --project-version 0.0.1-SNAPSHOT --doc=false

clean-parent-lambda:
  scala-cli clean parent-lambda
  rm -rf parent-lambda/generated

prepare-infra:
  #!/usr/bin/env bash
  # TODO: Should generate sources from derived child and parent schemas
  rm -rf infra/generated

  scala-cli run \
    --dep org.virtuslab::yaga-codegen-aws:{{yaga-version}} \
    --main-class yaga.codegen.aws.runCodegen \
    -- \
    --maven-artifact org.virtuslab:child-lambda_3:0.0.1-SNAPSHOT \
    --package-prefix childlambda \
    --output-dir $(pwd)/infra/generated/child-lambda/ \
    --with-infra

  scala-cli run \
    --dep org.virtuslab::yaga-codegen-aws:{{yaga-version}} \
    --main-class yaga.codegen.aws.runCodegen \
    -- \
    --maven-artifact org.virtuslab:parent-lambda_3:0.0.1-SNAPSHOT \
    --package-prefix parentlambda \
    --output-dir $(pwd)/infra/generated/parent-lambda/ \
    --with-infra

compile-infra:
  scala-cli compile infra

clean-infra:
  scala-cli clean infra
  rm -rf infra/generated

build-example:
  #!/usr/bin/env bash
  just publish-local-assembled-child-lambda
  just prepare-parent-lambda
  just publish-local-assembled-parent-lambda
  just prepare-infra
  just compile-infra

clean-example: clean-child-lambda clean-parent-lambda clean-infra
  rm -rf .out


infra-up:
  #!/usr/bin/env bash
  cd infra
  PULUMI_CONFIG_PASSPHRASE="" pulumi up --stack dev

infra-up-sbt:
  #!/usr/bin/env bash
  PULUMI_CONFIG_PASSPHRASE="" BESOM_SBT_MODULE=infra pulumi up --stack dev

infra-down:
  #!/usr/bin/env bash
  cd infra
  PULUMI_CONFIG_PASSPHRASE="" pulumi down --stack dev

infra-down-sbt:
  #!/usr/bin/env bash
  PULUMI_CONFIG_PASSPHRASE="" BESOM_SBT_MODULE=infra pulumi down --stack dev

infra-output:
  #!/usr/bin/env bash
  cd infra
  PULUMI_CONFIG_PASSPHRASE="" pulumi stack output --stack dev

infra-output-sbt:
  #!/usr/bin/env bash
  PULUMI_CONFIG_PASSPHRASE="" BESOM_SBT_MODULE=infra pulumi stack output --stack dev
